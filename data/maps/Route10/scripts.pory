mapscripts Route10_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Route10_OnTransition
}

script Route10_OnTransition {
    if (!flag(FLAG_CLEARED_SCORCHED_CHAMBERS)) {
        setflag(FLAG_TEMP_A)

        // consolidate seperate story beats
        if (flag(FLAG_CLEARED_OCHRE_VALE_MINE) && defeated(TRAINER_ROUTE10_MAGMA_GRUNT) && var(VAR_EVELYNN_STATE) == 4) {
            setflag(FLAG_TEMP_B)
        }

    } else {
        setvar(VAR_TEMP_A, 1)
    }
}

script Route10_Sign {
    msgbox("Soothing Sands Resort.", MSGBOX_SIGN)
}

script Route10_ResortOwner {
    msgbox(Route10_ResortOwner_Text, MSGBOX_NPC)
}

text Route10_ResortOwner_Text {
    "Hey there!\nI'm the owner of the resort here.\p"
    "Thank you for clearing out Team Magma,\nthose guys made business really awkward.\p"
    "But thanks to you, I no longer have to\nworry about my customers being harassed!\p"
    "You're free to go wherever in the\nresort you please, thanks again!"
}

script Route10_SandEnjoyer1 {
    msgbox(format("The warm sand really soothes my old joints."), MSGBOX_NPC)
}

script Route10_SandEnjoyer2 {
    msgbox(format("The trek to get here makes this place feel all the more special, so it's worth it."), MSGBOX_NPC)
}

script Route10_SandEnjoyer3 {
    msgbox(format("My late husband and I used to come here every week, but it's just not the same without him."), MSGBOX_NPC)
}

script Route10_SandEnjoyer4 {
    msgbox("I can't believe we managed\nto get a spot here!\pMy boyfriend is the best!", MSGBOX_NPC)
}

script Route10_SandEnjoyer5 {
    msgbox(format("...this was so expensive..."), MSGBOX_NPC)
}

script Route10_SandEnjoyer6 {
    msgbox(format("There's sand here, but it's not a beach?"), MSGBOX_NPC)
}

script Route10_SandEnjoyer7 {
    msgbox(format("I'm not sure these sand beds are designed for someone of my stature."), MSGBOX_NPC)
}

script Route10_SandMagmaGrunt {
    msgbox("Can't a girl enjoy the warm sand here!?\nI'd never be able to otherwise!", MSGBOX_NPC)
}

script Route10_Numel {
    setseenmon(SPECIES_NUMEL)
    playmoncry(SPECIES_NUMEL, CRY_MODE_NORMAL)
    msgbox("?", MSGBOX_NPC)
}

script Route10_MagmaGrunt {
    lockall
    msgbox(format("We dropped our pokeballs and one of our numels escaped, but we can't remember who's it is since we have so many..."))
    applymovement(24, moves(face_up))
    waitmovement(0)
    if (!defeated(TRAINER_ROUTE10_MAGMA_GRUNT)) {
        delay(60)
        applymovement(24, moves(face_player))
        waitmovement(0)
        trainerbattle_single(
            TRAINER_ROUTE10_MAGMA_GRUNT, 
            format("Wait a second, who are you!?"),
            format("Darn, if only I wasn't under contract to battle people.")  
        )
    }
    releaseall
}

script Route10_Hiker {
    trainerbattle_single(
        TRAINER_ROUTE10_HIKER, 
        format("There sure are a lot of old people around here."),
        format("I'm not that old yet right?")  
    )
    msgbox(format("The resort here is really popular with the elderly folks."), MSGBOX_AUTOCLOSE)
    end
}

script Route10_Youngster {
    trainerbattle_single(
        TRAINER_ROUTE10_YOUNGSTER, 
        format("Aha! You can give me something to do!"),
        format("Aw man, losing is no fun...")  
    )
    msgbox(format("My Grandma is at the resort, but there's nothing for me to do here."), MSGBOX_AUTOCLOSE)
    end
}

script Route10_RemoveBlocker {
    setvar(VAR_TEMP_A, 1)
}

script Route10_Guard {
    msgbox("I'm here to stop people who\ndon't have a booking.\pAnyone who enters from here\nhas to go through me.", MSGBOX_NPC)
}

script Route10_ResortBlocker {
    lockall    
    playse(SE_PIN)
    waitmovement(0)
    applymovement(18, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(18, moves(face_left))
    message("Hey!")
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up face_right))
    waitmovement(0)
    applymovement(18, Common_Movement_Delay48)
    waitmovement(0)
    closemessage
    msgbox("Sorry, but you don't have a booking,\nso you're not allowed to go in.")
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    waitmovement(0)
    releaseall
}