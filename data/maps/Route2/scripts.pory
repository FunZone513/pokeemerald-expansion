mapscripts Route2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Route2_StoryCheck
}

script Route2_StoryCheck {
    if (var(VAR_EVELYNN_STATE) > 0) {
        setflag(FLAG_TEMP_A)
    }
}

script Route2_Trigger_CamperL {
    call(Route2_MoveCamper)
}

script Route2_Trigger_CamperR {
    setvar(VAR_TEMP_0, 2)
    call(Route2_MoveCamper)
}

script Route2_MoveCamper {
    lockall
    applymovement(1, moves(face_down))
    playse(SE_PIN)
    waitmovement(0)
    applymovement(1, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(1, Common_Movement_Delay48)
	waitmovement(0)
    if (var(VAR_TEMP_0) == 2) {
        applymovement(1, moves(
            walk_right*2
            walk_down*2
        ))
    } else {
        applymovement(1, moves(
            walk_right
            walk_down*2
        ))
    }
    waitmovement(0)
    msgbox(Route2_Camper_Warning_Text, MSGBOX_NPC)
    if (var(VAR_TEMP_0) == 2) {
        applymovement(1, moves(
            walk_up*2
            walk_left*2
        ))
    } else {
        applymovement(1, moves(
            walk_up*2
            walk_left
        ))
    }
    setvar(VAR_LITTLEROOT_INTRO_STATE, 4)
    waitmovement(0)
    applymovement(1, moves(face_down))
    releaseall
}

text Route2_Camper_Warning_Text {
    "Hey there, I see you've just come from\nNewstem Town.\p"
    "I'm here to warn travellers about an\naggressive Pokemon just up ahead.\p"
    "I'm not sure why it's so upset,\nbut I would recommend avoiding it.\p"
    "Gym Leader Martin is supposed to be\ncoming to help calm it down,\l"
    "but I haven't heard from him in a bit.\p"
    "Say, could you go and\n find out where he is?\p"
    "It would be really helpful,\nas I'm stuck here making sure it\l"
    "doesn't hurt anyone.\p"
    "Best of luck!"
}

script Route2_Camper {
    msgbox("I hope you can get the Gym Leader to\n come soon, I'd like to go back home.", MSGBOX_NPC)
}

script Route2_Miniboss {
    lockall
    applymovement(2, moves(face_player))
    waitmovement(0)
    playmoncry(SPECIES_PARASECT, CRY_MODE_ENCOUNTER)
    setseenmon(SPECIES_PARASECT)
    msgbox("The Pokemon seems agitated!\nIt might attack if provoked!")
    waitmoncry
    msgbox("Will you leave it alone?", MSGBOX_YESNO)
    if (var(VAR_RESULT)) {
        msgbox("Probably for the best.")
    } else {
        msgbox("The Pokemon attacked!")
        call(Route2_Miniboss_Battle)
    }
    releaseall
}

script Route2_Miniboss_Battle {
    setwildbattle(SPECIES_PARASECT, 25, ITEM_OCCA_BERRY)
    setflag(FLAG_ACTIVE_BOSS_BATTLE)
    dowildbattle
    clearflag(FLAG_ACTIVE_BOSS_BATTLE)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON) {
        setflag(FLAG_HIDE_ROUTE2_MINIBOSS)
        setvar(VAR_LITTLEROOT_INTRO_STATE, 6)
        removeobject(2)
    } 
    releaseall
}

script Route2_BlackBelt {
    msgbox("I WISH I KNEW HOW TO SWIM!", MSGBOX_NPC)
}

script Route2_Evelynn {
    lock
    msgbox("You poor thing,\nwhat did those idiots do to you?")
    playse(SE_PIN)
    applymovement(6, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(6, moves(face_down))
    waitmovement(0)
    applymovement(6, Common_Movement_Delay48)
	waitmovement(0)
    msgbox("Can I help you?\p...\pOh wait,\nI don't care.")
    applymovement(6, moves(walk_up*5))
    waitmovement(0)
    removeobject(6)
    setflag(FLAG_TEMP_A)
    setvar(VAR_EVELYNN_STATE, 1)
    release
}