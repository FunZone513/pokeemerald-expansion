mapscripts SpringwoodCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SpringwoodCity_OnTransition
}

script SpringwoodCity_OnTransition {
    if (flag(FLAG_CLEARED_QUIET_GROVE) && var(VAR_EVELYNN_STATE) == 2 && flag(FLAG_BADGE01_GET)) {
        setvar(VAR_TEMP_A, 1)
    }
}

script SpringwoodCity_LibraEvelynnTriggerL {
    addobject(14)
    setobjectxy(14, 35, 21)
    call(SpringwoodCity_LibraEvelynnTrigger)
}

script SpringwoodCity_LibraEvelynnTriggerR {
    addobject(14)
    setobjectxy(14, 36, 21)
    call(SpringwoodCity_LibraEvelynnTrigger)
}

script SpringwoodCity_LibraEvelynnTrigger {
    lock
    hidefollower
    setvar(VAR_TEMP_A, 0)
    applymovement(14, moves(walk_down))
    playse(SE_WALL_HIT)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction walk_down unlock_facing_direction))
    waitmovement(0)
    playse(SE_PIN)
    waitmovement(0)
    applymovement(14, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(14, moves(face_down))
    waitmovement(0)
    msgbox("What the-\nSo you're following me now?\p...\pNot a very talkative one are you?")
    applymovement(14, moves(face_left))
    waitmovement(0)
    applymovement(14, moves(delay_16 delay_16))
    waitmovement(0)
    applymovement(14, moves(face_right))
    waitmovement(0)
    applymovement(14, moves(delay_16 delay_16))
    waitmovement(0)
    applymovement(14, moves(face_down))
    waitmovement(0)
    msgbox(SpringwoodCity_LibraEvelynn_Text)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right lock_facing_direction walk_left unlock_facing_direction))
    applymovement(14, moves(walk_down*7))
    waitmovement(0)
    removeobject(14)
    setvar(VAR_EVELYNN_STATE, 3)
    release
}

text SpringwoodCity_LibraEvelynn_Text {
    "Wait... Are you the one that\ncleared out Quiet Grove?\p"
    "Hmmm...\p"
    "I'm not sure what you're after,\nbut if you're against Team Aqua then\lyou can't be all bad.\p"
    "Tell you what, I'll be in Spring Woods\nto check something.\p"
    "Be there."
}

script SpringwoodCity_Intro_Scientist {
    lockall
    if (var(VAR_LITTLEROOT_INTRO_STATE) == 4) {
        msgbox(SpringwoodCity_Intro_Scientist_Text1)
        playse(SE_PIN)
        applymovement(1, Common_Movement_ExclamationMark)
        waitmovement(0)
        faceplayer
        delay(24)
        msgbox(SpringwoodCity_Intro_Scientist_Text2)
        msgbox(SpringwoodCity_Intro_Scientist_Text3, MSGBOX_NPC)
        applymovement(1, moves(face_up))
        waitmovement(0)
        setvar(VAR_LITTLEROOT_INTRO_STATE, 5)
    } else {
        msgbox("Route 4 is straight west from here,\njust past Route 3. Good Luck!", MSGBOX_NPC)
    }
    applymovement(2, moves(face_up))
    waitmovement(0)
    releaseall
}

text SpringwoodCity_Intro_Scientist_Text1 {
    "Oh man...\nI forgot to tell the director\l"
    "that I left for Springwood Gym.\p"
    "I don't want to get in trouble,\nbut I also can't afford to waste time.\p"
    "I hope the current challenger doesn't\ntake too long."
}

text SpringwoodCity_Intro_Scientist_Text2 {
    "Oh!\l"
    "I'm sorry I didn't notice you were there.\p"
    "I'm waiting to get into the Gym,\nbut someone is currently doing the\lGym Challenge.\p"
    "So I'm stuck out here waiting\nfor them to finish because I need\lto speak to the Gym Leader."
}

text SpringwoodCity_Intro_Scientist_Text3 {
    "...\p"
    "What's that? You need to speak to\nhim about a Pokemon on Route 2?\p"
    "What a coincidence!\nThat's also why I'm here.\p"
    "He needed us to make a special potion\nto calm the Pokemon down, so I'm here\lto deliver it to him.\p"
    "Tell you what, can you go and tell the\nDirector at the Research Lab on Route 4\lthat the potion has been delivered?\p"
    "I'll make sure the Gym Leader\nclears out the Pokemon on Route 2\lafter the challenger leaves.\p"
    "Thanks in advance!"
}

script SpringwoodCity_Sign_Name {
    msgbox("Springwood City\nA bustling coastal beauty.", MSGBOX_SIGN)
}

script SpringwoodCity_Sign_Park {
    msgbox(format("A small piece of nature, nurtured by us."), MSGBOX_SIGN)
}

script SpringwoodCity_Sign_BigBuilding {
    msgbox("Libra Institute\nBalancing science and nature.", MSGBOX_SIGN)
}

script SpringwoodCity_Sign_Gym {
    msgbox("Springwood Gym\nHome of the strong.", MSGBOX_SIGN)
}

script SpringwoodCity_Sydney {
    lockall
    if (!flag(FLAG_ITEM_SPRINGWOOD_CITY_SILVER_POWDER)) {
        faceplayer
        msgbox("Psst, hey kid.")
        msgbox("Have you heard?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            msgbox("Yeah you sure have.", MSGBOX_NPC)
            giveitem(ITEM_SILVER_POWDER, 1)
            setflag(FLAG_ITEM_SPRINGWOOD_CITY_SILVER_POWDER)
        } else { 
            msgbox("Bah!\nWasting my time.", MSGBOX_NPC)
        }
    } else {
        msgbox("You didn't get it from me.", MSGBOX_NPC)
    }
    releaseall
}

script SpringwoodCity_RichBoy {
    msgbox(format("Springwood City might be on a seaside cliff, but that doesn't mean we get much salt spray. We must be too high up."), MSGBOX_NPC)
}

script SpringwoodCity_Artist {
    msgbox(format("When I get into a creative slump, I find it nice to come and chill at the park."), MSGBOX_NPC)
}

script SpringwoodCity_Beauty {
    msgbox(format("For such a large city there really isn't very much in the way of shopping is there?"), MSGBOX_NPC)
}

script SpringwoodCity_Athlete {
    msgbox(format("Running in the city can be difficult with all the people, but I'm scared of bug types so I can't run on any of the nearby routes..."), MSGBOX_NPC)
}

script SpringwoodCity_Camper {
    msgbox(format("Springwood City is just inland from a coastal cliffside, so make sure to watch your step!"), MSGBOX_NPC)
}

script SpringwoodCity_Boy {
    msgbox("Did you know that some Routes have super\nrare Pokemon?\pI wonder what Pokemon live around\nhere that I've never seen before.", MSGBOX_NPC)
}

script SpringwoodCity_Sailor {
    msgbox("The bustle of Springwood City is real\ndifferent to the port town I'm from.", MSGBOX_NPC)
}

script SpringwoodCity_Fisherman {
    msgbox("I'm sure that one of these days I'll\ncatch some super rare Pokemon that can\lonly be found in Springwood City's\lright-hand fountain!", MSGBOX_NPC)
}

script SpringwoodCity_AquaGrunt1 {
    msgbox(format("What? Do you mind?"), MSGBOX_NPC)
}

script SpringwoodCity_AquaGrunt2 {
    msgbox(format("You can't just barge into people's conversations like that, very rude."), MSGBOX_NPC)
}

script SpringwoodCity_AquaGrunt3 {
    msgbox(format("I'm not doing anything weird, just hangin' out."), MSGBOX_NPC)
}

script SpringwoodCity_AquaGrunt4 {
    msgbox(format("You haven't seen anyone wearing red anywhere nearby have you?"), MSGBOX_NPC)
}

script SpringwoodCity_AquaGrunt5 {
    msgbox(format("Buh?"), MSGBOX_NPC)
}