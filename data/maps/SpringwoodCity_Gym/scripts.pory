mapscripts SpringwoodCity_Gym_MapScripts {
    MAP_SCRIPT_ON_LOAD: SpringwoodCity_Gym_Reset
}

script SpringwoodCity_Gym_Reset {
    // tiles
    setmetatile(3, 15, METATILE_MauvilleGym_RaisedSwitch, FALSE)
    setmetatile(2, 10, METATILE_MauvilleGym_RaisedSwitch, FALSE)
    setmetatile(10, 7, METATILE_MauvilleGym_RaisedSwitch, FALSE)
    // barrier
    setmetatile(6, 5, METATILE_MauvilleGym_GreenBeamH1_On, TRUE)
    setmetatile(7, 5, METATILE_MauvilleGym_GreenBeamH2_On, TRUE)
    setmetatile(6, 6, METATILE_MauvilleGym_GreenBeamH3_On, TRUE)
    setmetatile(7, 6, METATILE_MauvilleGym_GreenBeamH4_On, TRUE)
}

script SpringwoodCity_Gym_CheckPlayerPos {
    getobjectxy(OBJ_EVENT_ID_PLAYER, CURRENT_POSITION, VAR_TEMP_A, VAR_TEMP_B)
    if (var(VAR_TEMP_A) == 3 && var(VAR_TEMP_B) == 15) { 
        call(SpringwoodCity_Gym_PressSwitch1) 
    } elif ( var(VAR_TEMP_A) == 2 && var(VAR_TEMP_B) == 10 ) {
        call(SpringwoodCity_Gym_PressSwitch2)
    } elif ( var(VAR_TEMP_A) == 10 && var(VAR_TEMP_B) == 7 ) {
        call(SpringwoodCity_Gym_PressSwitch3)
    }
}

script SpringwoodCity_Gym_PressSwitch1 {
    if (!var(VAR_TEMP_1)) {
        playse(SE_UNLOCK)
        setvar(VAR_TEMP_1, TRUE)
        setmetatile(3, 15, METATILE_MauvilleGym_PressedSwitch, FALSE)
        call(SpringwoodCity_Gym_CheckState)
    }
}

script SpringwoodCity_Gym_PressSwitch2 {
    if (!var(VAR_TEMP_2)) {
        playse(SE_UNLOCK)
        setvar(VAR_TEMP_2, TRUE)
        setmetatile(2, 10, METATILE_MauvilleGym_PressedSwitch, FALSE)
        call(SpringwoodCity_Gym_CheckState)
    }
}

script SpringwoodCity_Gym_PressSwitch3 {
    if (!var(VAR_TEMP_3)) {
        playse(SE_UNLOCK)
        setvar(VAR_TEMP_3, TRUE)
        setmetatile(10, 7, METATILE_MauvilleGym_PressedSwitch, FALSE)
        call(SpringwoodCity_Gym_CheckState)
    }
}

script SpringwoodCity_Gym_CheckState {
    if (var(VAR_TEMP_1) && var(VAR_TEMP_2) && var(VAR_TEMP_3)) { 
        setmetatile(6, 5, METATILE_MauvilleGym_GreenBeamH1_Off, FALSE)
        setmetatile(7, 5, METATILE_MauvilleGym_GreenBeamH2_Off, FALSE)
        setmetatile(6, 6, METATILE_MauvilleGym_GreenBeamH3_Off, FALSE)
        setmetatile(7, 6, METATILE_MauvilleGym_GreenBeamH4_Off, FALSE)
    }
    special(DrawWholeMapView)
}

script Springwood_Gym_Guide {
    lockall
    if (flag(FLAG_TEMP_1)) {  
        msgbox(Springwood_Gym_Guide_Text_Intro)
    }
    msgbox(Springwood_Gym_Guide_Text, MSGBOX_NPC)
    releaseall
}

text Springwood_Gym_Guide_Text_Intro {
    "A new face!\nI'm the guide here at Springwood Gym.\p"
    "My job is to make sure the challengers\nknow what it is they are up against.\p"
    "Each Gym is assigned a guide to help\nout newcomers to the Gym Challenge."
}

text Springwood_Gym_Guide_Text {
    "Here at the Springwood Gym,\nGym Leader Martin likes to use Pokemon\lthat he found nearby.\p"
    "He believes that after a bit of care,\nany Pokemon can become strong!"
}

script SpringwoodCity_Gym_Martin {
    //lockall
    trainerbattle_single(
        GYM_LEADER_MARTIN,
        SpringwoodCity_Gym_Martin_Text_Intro,
        SpringwoodCity_Gym_Martin_Text_Defeated,
        SpringwoodCity_Gym_Martin_Defeated,
        NO_MUSIC
    )
    msgbox(SpringwoodCity_Gym_Martin_PostBattle, MSGBOX_NPC)
    releaseall
}

script SpringwoodCity_Gym_Martin_Defeated {
    message("Recieved the Springwood Badge!")
	waitmessage
	call(Common_EventScript_PlayGymBadgeFanfare)
    msgbox(SpringwoodCity_Gym_Martin_Badge, MSGBOX_DEFAULT)
	setflag(FLAG_DEFEATED_RUSTBORO_GYM)
	setflag(FLAG_BADGE01_GET)
    releaseall
}

text SpringwoodCity_Gym_Martin_Text_Intro {
    "Ah, you must be {PLAYER}.\nI heard that you helped with the rowdy\lPokemon on Route 2.\p"
    "I've been looking forward to meeting you,\nI hope you found some of the Pokemon\lnearby interesting like I have.\p"
    "I've trained my Pokemon as much as\nI could without going beyond what's\lexpected of the first Gym Leader.\p"
    "I hope you don't disappoint,\nI'm excited to see what you've found."
}

text SpringwoodCity_Gym_Martin_Text_Defeated {
    "Outstanding.\p"
    "Even after all my training you still\nfound a way to best me at my own game.\p"
    "I wouldn't have it any other way,\nI hope you continue to make good progress\ltogether with your Pokemon."
}

text SpringwoodCity_Gym_Martin_PostBattle {
    "Grass and Bug types, as well as\nPokemon that have an ability related to\lcutting are able to cut down trees.\p"
    "Make sure to eep an eye out for any\ncool Pokemon for our next battle!\p"
    "I'm excited to see just how much\nstronger your team can get!"
}

text SpringwoodCity_Gym_Martin_Badge {
    "Having the Springwood Badge means\nthat your Pokemon are able to cut down\lsmall trees that may block your path.\p"
    "Grass and Bug types, as well as any\nPokemon that have an ability related to\lcutting are able to help you."
}

script SpringwoodGym_Trainer1 {
    trainerbattle_single(
        SPRINGWOOD_GYM_TRAINER1,
        "Martin only lets us use Pokemon from\nnearby routes, let's see how your Pokemon\lcompare to ours.",
        "You've certainly picked the good ones...",
        SpringwoodCity_Gym_CheckPlayerPos
    )
    msgbox("I just started here, so I haven't got\nthe skill of the other trainers yet.", MSGBOX_AUTOCLOSE)
    end
}

script SpringwoodGym_Trainer2 {
    trainerbattle_single(
        SPRINGWOOD_GYM_TRAINER2, 
        "There's not very many elegant Pokemon\nto be found around here.",
        "At least theres a certain elegance to\nbe found in defeat.",
        SpringwoodCity_Gym_CheckPlayerPos
    )
    msgbox("While your looks may not be elegant,\nyour battle skill certainly is.", MSGBOX_AUTOCLOSE)
    end
}

script SpringwoodGym_Trainer3 {
    trainerbattle_single(
        SPRINGWOOD_GYM_TRAINER3,
        "I TRAIN EXCLUSIVELY IN\nDRIPSTONE CAVE!",
        "I'D BETTER GET BACK ON THE GRIND!",
        SpringwoodCity_Gym_CheckPlayerPos
    )
    msgbox("My therapist tells me I need to calm\ndown, but training Pokemon gets me HYPE!", MSGBOX_AUTOCLOSE)
    end
}

script SpringwoodGym_Trainer4 {
    trainerbattle_single(
        SPRINGWOOD_GYM_TRAINER4,
        "Bug types are great for having strong\nPokemon at low levels.",
        "...maybe they could do with a few more\nlevels...",
        SpringwoodCity_Gym_CheckPlayerPos
    )
    msgbox("Maybe I can re-use this net to catch\nmyself a girlfriend...", MSGBOX_AUTOCLOSE)
    end
}