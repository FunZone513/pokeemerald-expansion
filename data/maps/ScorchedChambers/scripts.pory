mapscripts ScorchedChambers_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: ScorchedChambers_OnTransition
}

script ScorchedChambers_OnTransition {
    if (!flag(FLAG_CLEARED_SCORCHED_CHAMBERS)) {
        setflag(FLAG_TEMP_A) // hides the regular trainers
        if (flag(FLAG_SCORCHED_CHAMBERS_BRIDGE_SEEN)) {
            setvar(VAR_TEMP_A, 2)
        } else { setvar(VAR_TEMP_A, 1) } // evelynn trigger
    }
}

script ScorchedChambers_Hiker {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_HIKER, 
        format("I like caves, but boy this one is hard to be in."),
        format("I think I need to get fresh air...")  
    )
    msgbox(format("The heat in here is something else..."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_Picnicker {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_PICNICKER, 
        format("I regret coming in here with such thick clothing."),
        format("I think my Pokemon have heatstroke...")  
    )
    msgbox(format("I wish I could stick my head into a fridge right now."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_Guitarist {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_GUITARIST, 
        format("We find our greatest tunes in the face of adversity."),
        format("That's not very rock of you...")  
    )
    msgbox(format("My sweat is making it hard to play the notes I want."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_BugCatcher {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_BUG_CATCHER, 
        format("I think I came to the wrong place."),
        format("Bugs and lava, not a great mix.")  
    )
    msgbox(format("I thought training here would be a good test for my bugs..."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_Fisherman {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_FISHERMAN, 
        format("I don't seem to be catching very much, not sure why."),
        format("Well.")  
    )
    msgbox(format("What? Lava?"), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_BlackBelt {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_BLACKBELT, 
        format("SUFFERING BUILDS CHARACTER!"),
        format("I GOT SWEAT IN MY EYES AHHHHH!!!")  
    )
    msgbox(format("I think the intense heat might be impacting my mental faculties."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_BattleGirl {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_BATTLEGIRL, 
        format("What is that guy yelling about?"),
        format("...is he okay?")  
    )
    msgbox(format("...oh man I'm so sticky with sweat..."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_ExpertF {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_EXPERTF, 
        format("Ah, you also fell down."),
        format("Well, you sure are energetic.")  
    )
    msgbox(format("I fell down this hole a while ago, but it's like a sauna in here so it's alright."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_ExpertM {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_EXPERTM, 
        format("Oh dear I think I put my back out."),
        format("I'm much too old to be falling into caves...")  
    )
    msgbox(format("I might be too sore to make my way out..."), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_Camper {
    msgbox("Apparently you can jump down from here,\nbut it looks a little high for me...", MSGBOX_NPC)
}

script ScorchedChambers_MagmaGrunt1 {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_MAGMA_GRUNT1, 
        format("Hey! You're not supposed to be in here!"),
        format("Well shoot, I guess I can't stop you...")  
    )
    msgbox(format("I'm supposed to stop people from coming in, but what can I do if they just beat me up?"), MSGBOX_AUTOCLOSE)
    end
}
script ScorchedChambers_MagmaGrunt2 {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_MAGMA_GRUNT2, 
        format("Huh? Is Fred not doing his job?"),
        format("Oh, well that explains it.")  
    )
    msgbox(format("Geez he could have at least alerted us that someone got in."), MSGBOX_AUTOCLOSE)
    end
}
script ScorchedChambers_MagmaGrunt3 {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_MAGMA_GRUNT3, 
        format("WAH! Where did you come from?"),
        format("This isn't it...")  
    )
    msgbox(format("I'm trying to stay cool back here but man, I'm sweltering."), MSGBOX_AUTOCLOSE)
    end
}
script ScorchedChambers_MagmaGrunt4 {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_MAGMA_GRUNT4, 
        format("If only my arms were a little longer!"),
        format("Curse my tiny Tyrunt arms...")  
    )
    msgbox(format("Idk what that item is but I sure do want it."), MSGBOX_AUTOCLOSE)
    end
}
//TODO this is meant to call over the other one, its the pair from ochre vale

script ScorchedChambers_MagmaGrunt5 {
    


    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_MAGMA_GRUNT5, 
        format(""),
        format("")  
    )
    msgbox(format(""), MSGBOX_AUTOCLOSE)
    end
}
script ScorchedChambers_MagmaGrunt6 {
    trainerbattle_single(
        TRAINER_SCORCHED_CHAMBERS_MAGMA_GRUNT6, 
        format(""),
        format("")  
    )
    msgbox(format(""), MSGBOX_AUTOCLOSE)
    end
}

script ScorchedChambers_AlertEvelynn {
    lock
    setvar(VAR_TEMP_A, 2)
    playse(SE_PIN)
    applymovement(24, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(24, moves(face_down))
    waitmovement(0)
    message("HEY! KID!")
    waitmessage
    delay(24)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
    waitmovement(0)
    getobjectxy(OBJ_EVENT_ID_PLAYER, CURRENT_POSITION, VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_2) == 49) {
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
    } else { applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up)) }
    waitmovement(0)
    closemessage
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up))
    waitmovement(0)
    msgbox("What are you even doing here?\p...\pHuh? I can't hear you from down here!")
    applymovement(24, moves(face_left delay_16*4 face_right delay_16*4 face_down))
    waitmovement(0)
    msgbox("Wait right there!\nI'll come up!")
    applymovement(24, moves(walk_fast_up))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down))
    waitmovement(0)
    special(RemoveCameraObject)
    delay(48)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    waitmovement(0)
    delay(48)
    setobjectxy(24, 39, 52)
    applymovement(24, moves(walk_left walk_up*4 walk_left*7))
    waitmovement(0)
    msgbox(ScorchedChambers_EvelynnText)
    applymovement(24, moves(walk_down walk_fast_left))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(24, moves(walk_fast_left))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
    applymovement(24, moves(walk_fast_left*7))
    waitmovement(0)
    removeobject(24)
    release
}

script ScorchedChambers_MagmaBridge {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
    waitmovement(0)
    getobjectxy(OBJ_EVENT_ID_PLAYER, CURRENT_POSITION, VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_1) == 49) { 
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
        waitmovement(0)
    }
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up*2))
    waitmovement(0)
    if (flag(FLAG_SCORCHED_CHAMBERS_BRIDGE_SEEN)) { 
        special(HealPlayerParty)
        applymovement(27, moves(walk_up))
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
        waitmovement(0)
        goto(ScorchedChambers_MagmaMultiBattle) // skip if you lost previously | lmao loser
    }
    applymovement(27, moves(face_right))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
    waitmovement(0)
    msgbox(ScorchedChambers_EvelynnText2)
    playse(SE_USE_ITEM)
    waitse
    special(HealPlayerParty)
    delay(10)
    applymovement(27, moves(face_up))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    waitmovement(0)
    msgbox("Now that you've had your little chat,\nlet's get down to business.")
    applymovement(27, moves(walk_up))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
    waitmovement(0)
    goto(ScorchedChambers_MagmaMultiBattle)
}

script ScorchedChambers_MagmaMultiBattle {
    setflag(FLAG_SCORCHED_CHAMBERS_BRIDGE_SEEN)
    special(SavePlayerParty) // save the normal party
    fadescreen(FADE_TO_BLACK)
    special(ChooseHalfPartyForBattle) // pick your pokemon
    waitstate
    if (var(VAR_RESULT) == 0) {
        special(LoadPlayerParty)
        applymovement(27, moves(face_right))
        waitmovement(0)
        msgbox("What are you doing?\nWe have to fight them!")
        applymovement(27, moves(face_up))
        waitmovement(0)
        goto(ScorchedChambers_MagmaMultiBattle)
    }
    multi_2_vs_2(
        TRAINER_SCORCHED_CHAMBERS_MAGMA_ADMIN, 
        "How did you...", 
        TRAINER_SCORCHED_CHAMBERS_MAGMA_GRUNT7, 
        "My promotion... I knew\nTeam Magma didn't have job security!",
        PARTNER_SCORCHED_CHAMBERS_EVELYNN
    )
    if (var(VAR_RESULT) != 1) {
        fadescreen(FADE_TO_BLACK)
        special(SetCB2WhiteOut)
        waitstate
    }
    applymovement(25, moves(lock_facing_direction walk_up*2 unlock_facing_direction))
    applymovement(26, moves(lock_facing_direction walk_up*2 unlock_facing_direction))
    applymovement(27, moves(walk_up*2))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up*2))
    waitmovement(0)
    delay(20)
    applymovement(18, moves(face_down))
    waitmovement(0)
    msgbox("Haa...\pYou're dismissed.")
    playse(SE_PIN)
    applymovement(25, Common_Movement_ExclamationMark)
    applymovement(26, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(16)
    applymovement(25, moves(face_up))
    applymovement(26, moves(face_up))
    waitmovement(0)
    msgbox("Yes sir!")
    applymovement(25, moves(walk_fast_left*8))
    applymovement(26, moves(walk_fast_left*8))
    delay(30)
    applymovement(27, moves(walk_up))
    waitmovement(26)
    removeobject(25)
    removeobject(26)
    msgbox("Maxie!\pWhat are you up to now?!")
    delay(20)
    special(SpawnCameraObject)
    applymovement(18, moves(face_up))
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up*4))
    waitmovement(0)
    msgbox("Doesn't it seem strange, Miss Evelynn?\p")
    applymovement(27, moves(walk_up))
    msgbox("What does?")
    msgbox("Everything.\pThe warmth of this lava,\nthe strength of our Pokemon,\leven our very lives.")
    msgbox("Have you gone mad?")
    applymovement(18, moves(face_down))
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_fast_down*2))
    waitmovement(0)
    msgbox(ScorchedChambers_MaxieText)
    applymovement(27, moves(lock_facing_direction walk_down*2 unlock_facing_direction))
    msgbox("I...\pOf course I know that!\pWho wouldn't know their own name?!")
    applymovement(18, moves(face_up))
    waitmovement(0)
    msgbox("Yes of course, just as my name is Maxie,\lyour name is Evelynn.")
    delay(120)
    applymovement(18, moves(face_down))
    waitmovement(0)
    msgbox("...\pI see it's still too early.")
    applymovement(18, moves(walk_down walk_left face_down))
    waitmovement(0)
    msgbox("I shall be taking my leave here,\nI hope to see you again Evelynn.")
    applymovement(18, moves(face_left delay_16*2 face_down))
    waitmovement(0)
    msgbox("And I hope to never see your annoying\nfriend again.")
    applymovement(18, moves(walk_left*8))
    waitmovement(0)
    removeobject(18)
    delay(60)
    applymovement(27, moves(face_right))
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down*2))
    waitmovement(0)
    msgbox("I uh...\pThanks for the help...")
    applymovement(27, moves(walk_left*8))
    waitmovement(0)
    removeobject(27)
    special(RemoveCameraObject)
    setflag(FLAG_CLEARED_SCORCHED_CHAMBERS)
    setvar(VAR_TEMP_A, 0)
}

text ScorchedChambers_EvelynnText {
    "I see that you've already taken care of\nsome of the guards.\p"
    "If I had known you would be doing that,\nI wouldn't have bothered sneaking in...\p"
    "Whatever,\nhow about a temporary alliance?\p"
    "I'm not really sure what all these\nTeam Magma goons are after,\lbut it can't be good, whatever it is.\p"
    "I think I saw their Boss up ahead,\nbut he's all mine to take care of,\lso if you don't mind..."
}

text ScorchedChambers_EvelynnText2 {
    "I know I know,\n I didn't make it very far.\p"
    "But I think we'd better work together\nto take down these two.\p"
    "I'll quickly heal your Pokemon,\nso you better not hold me back!"
}

text ScorchedChambers_MaxieText {
    "Far from it!\nI'd say you're the one who's mad here.\p"
    "Tell me, how much do you remember?\p"
    "Do you know where we are?\pDo you know who I am?\pDo you even know your name?"
}